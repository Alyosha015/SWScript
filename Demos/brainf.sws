;hello world program from wikipedia
local program = "+[-->-[>>+>-----<<]<--<---]>-.>>>+.>>..+++[.>]<<<<.+++.------.<<-.>>>>+."

local memory={}
local pointer=0

local loopMap={}

local tempLoopMap={}
local tempLoopMapSp=0

for i=0, i<30_000 {
    memory[i]=0
    loopMap[i]=0
}

Run()

func Run() {
    Init()
    for i=0, i<#program {
        local c = lua.string.sub(program, i+1, i+1)
        if c == '.' { print(lua.string.char(memory[pointer]))
        } else if c == ',' { ;impossible in SWS (unless lua input is used)
        } else if c == '+' { memory[pointer]++ if memory[pointer]>255 {memory[pointer] = 0}
        } else if c == '-' { memory[pointer]-- if memory[pointer]<0 {memory[pointer] = 255}
        } else if c == '>' { pointer++ if pointer>29999 {pointer=0}
        } else if c == '<' { pointer-- if pointer<0 {pointer=29999}
        } else if c == '[' && memory[pointer]==0 { i=loopMap[i]
        } else if c == ']' && memory[pointer]!=0 { i=loopMap[i] }
    }
}

func Init() {
    for i=0, i<#program {
        if lua.string.sub(program, i+1, i+1) == "[" {
            tempLoopMap[tempLoopMapSp++] = i
        } else if lua.string.sub(program, i+1, i+1) == "]" {
            local start = tempLoopMap[--tempLoopMapSp]
            loopMap[start] = i
            loopMap[i] = start
        }
    }
}